Name: 2xyter
Init:   c0
Accept: qa

//Checks for compatibility for autograder
c0,0
c1,0,>
c0,1
c1,1,>
c0,2
c1,2,>

c1,0
c1,0,>
c1,1
c1,1,>
c1,2
c1,2,>
c1,#
c2,#,>

c2,0
r1,0,<
c2,1
r1,1,<
c2,2
r1,2,<
///////////////////////////////


// Initialization for left side //
i0,0
i0,_,>
i0,1
i1,1,>
i0,2
i1,2,>
i0,#
i4,#,>

//Move to right side
i1,0
i1,0,>
i1,1
i1,1,>
i1,2
i1,2,>
i1,#
i2,#,>

//Initialization for right side
i2,0
i2,Y,>
i2,1
r2,1,<
i2,2
r2,2,<

//Zero string
i4,0
i4,Y,>
i4,_
qa,_,-
////////////////////////////////

//Move to get ready to x2
n0,0
n0,0,>
n0,1
n0,1,>
n0,2
n0,2,>
n0,_
n,_,<

//Next number no carry
n,0
n,0,<
n,1
n,2,<
n,2
c,1,<
n,_
q0,_,>

//Next number with carry
c,0
n,1,<
c,1
c,0,<
c,2
c,2,<
c,_
q0,1,-

/////////// START COMPARING TWO STRINGS ///////////
//Take new value
q0,0
m0,X,>
q0,1
m1,X,>
q0,2
m2,X,>
q0,#
s3,#,>

//Move with goal of 0
m0,1
m0,1,>
m0,0
m0,0,>
m0,2
m0,2,>
m0,#
s0,#,>
//Move with goal of 1
m1,1
m1,1,>
m1,0
m1,0,>
m1,2
m1,2,>
m1,#
s1,#,>
//Move with goal of 2
m2,1
m2,1,>
m2,0
m2,0,>
m2,2
m2,2,>
m2,#
s2,#,>

//Search with goal of 0
s0,Y
s0,Y,>
s0,0
r0,Y,<
//Search with goal of 1
s1,Y
s1,Y,>
s1,1
r0,Y,<
//Search with goal of 2
s2,Y
s2,Y,>
s2,2
r0,Y,<
//Search with goal of nothing more
s3,Y
s3,Y,>
s3,_
qa,_,-
///////////// COMPARE TWO STRINGS OVER //////////////

//Return to get new number
r0,Y
r0,Y,<
r0,#
r0,#,<
r0,0
r0,0,<
r0,1
r0,1,<
r0,2
r0,2,<
r0,X
q0,X,>
r0,_
q0,_,>

//Return to initialize
r1,#
r1,#,<
r1,0
r1,0,<
r1,1
r1,1,<
r1,2
r1,2,<
r1,_
i0,_,>

//Return to x2
r2,#
n,#,<
r2,Y
r2,Y,<
