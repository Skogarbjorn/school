"isort.mexe" = main in
{{
main = fun()
	{
		;;; Notkun: y = sort(x);
		;;; Fyrir:  x er listi talna.
		;;; Eftir:  y er listi sem inniheldur sömu gildi
		;;;         og x en í vaxandi röð.
		rec fun sort(x)
		{
			;;; Notkun: y = insert(x,v);
			;;; Fyrir:  x er listi talna, v er tala.
			;;;         x er í vaxandi röð.
			;;; Eftir:  y er listi í vaxandi röð sem
			;;;         inniheldur öll gildin í x auk 
			;;;         gildisins v.
			rec fun insert(x, v)
			{
				if (x == [])
				{
					return [v];
				};
				if (head(x) > v)
				{
					return v : x;
				};
				return head(x) : insert(tail(x), v);
			};
			if (x == [])
			{
				return x;
			};
			return insert(sort(tail(x)), head(x));
		};
	
		writeln(sort([1,5,3,2,45,2,6]));
		writeln(sort([]));
	};

}}
*
BASIS
;

